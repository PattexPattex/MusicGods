plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.pattexpattex'
version '0.2.1-hotfix1'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.chew.pro/snapshots' }
    maven { url 'https://m2.dv8tion.net/releases' }

    jcenter()

    flatDir {
        dirs 'lib'
    }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.1' //JDA
    implementation 'ch.qos.logback:logback-classic:1.4.5' //Logging
    implementation 'org.json:json:20220924' //org.json for config

    implementation('com.sedmelluq:jda-nas:1.1.0') { //Native audio send UDP queue
        exclude module: 'udp-queue'
    }
    //implementation 'club.minnced:udpqueue-api:0.1.1'
    implementation 'club.minnced:udpqueue-native-win-x86-64:0.2.6'

    implementation 'com.github.walkyst:lavaplayer-fork:1.3.98.4' //Lavaplayer
    implementation files('lib/lavadsp-0.7.7.jar') //Lavaplayer audio filters
    implementation files('lib/native-loader-0.7.0.jar')
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:7.2.2' //Spotify web API wrapper
    implementation 'com.jagrosh:JLyrics:0.6' //JLyrics
    implementation 'com.typesafe:config:1.4.2' //Typesafe (dependency of JLyrics)
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.sapher:youtubedl:1.1' //YoutubeDL wrapper
    implementation 'ch.obermuhlner:java-scriptengine:2.0.0' //Java script engine

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0' //Junit
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    manifest {
        attributes('Main-Class': 'com.pattexpattex.musicgods.Bot')
        attributes('Implementation-Version': project.version)
        attributes('Implementation-Title': project.name)
        attributes('Implementation-Vendor-Id': project.group)
    }
}

application {
    mainClass.set('com.pattexpattex.musicgods.Bot')
}

javadoc {
    options.encoding 'utf-8'
}

compileJava {
    options.compilerArgs << '-parameters'
    options.encoding 'utf-8'
}

compileTestJava {
    options.encoding 'utf-8'
}

test {
    useJUnitPlatform()
}

distTar.dependsOn(shadowJar)
distZip.dependsOn(shadowJar)
startScripts.dependsOn(shadowJar)
startShadowScripts.dependsOn(jar)
shadowDistTar.dependsOn(jar)
shadowDistZip.dependsOn(jar)

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}